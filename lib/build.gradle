buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'groovy'

configurations {
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.library.wj:dexknife-wj-libs:1.0.2'
    provided 'com.android.tools.build:builder:2.1.3'
    provided 'com.android.tools.build:gradle-core:2.1.3'
    compile 'net.lingala.zip4j:zip4j:1.3.2'
    compile 'ant:ant:1.7.0'
    compile 'dom4j:dom4j:20040902.021138'
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
tasks.withType(Javadoc) {
    enabled = false //禁用生成javadoc的任务
}


apply plugin: 'java'

task zipToShell(type: JavaExec, dependsOn: 'classes') {
    description '运行ShellUtil.java中main方法'
    classpath = sourceSets.main.runtimeClasspath
    main = "com.library.dexknife.doaction.ToZip"
}

task zipToApk(type: JavaExec, dependsOn: 'classes') {
    description '运行JiaGuUtil.java中main方法'
    classpath = sourceSets.main.runtimeClasspath
    main = "com.library.dexknife.doaction.ToJiaGu"
}